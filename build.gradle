allprojects {
    group = 'xyz.skywind'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

apply(plugin: 'idea')
apply(plugin: 'maven')

idea {
    project {
        languageLevel = '1.8'

        vcs = 'git'
    }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException("This build must be run with java 8. Current: " + JavaVersion.current())
}

def coreModule = project(':ray-core')
def annModule = project(':ray-annotation')
def xmlModule = project(':ray-xml')
def jsonModule = project(':ray-json')
def txtCommonsModule = project(':ray-txt-commons')

configure([coreModule, annModule, xmlModule, jsonModule, txtCommonsModule]) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    dependencies {
        testCompile 'junit:junit:4.10'
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(annModule) {
    dependencies {
        compile coreModule
    }
}

configure(txtCommonsModule) {
    dependencies {
        compile coreModule
    }
}

configure(jsonModule) {
    dependencies {
        compile txtCommonsModule
        compile "com.eclipsesource.minimal-json:minimal-json:0.9.1"
    }
}

configure(xmlModule) {
    dependencies {
        compile txtCommonsModule
    }
}